plugins {
    id "kotlin2js" version "1.3.40"
    id "org.jetbrains.kotlin.frontend" version "0.0.45"
    id "kotlin-dce-js" version "1.3.40"
}
group 'kotlin-poc-frontend-react-redux'
version '1.0-SNAPSHOT'

ext {
    web_dir = "web"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlin-js-wrappers" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.11.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    implementation 'org.jetbrains:kotlin-react:16.6.0-pre.72-kotlin-1.3.40'
    implementation 'org.jetbrains:kotlin-react-dom:16.6.0-pre.72-kotlin-1.3.40'
    implementation 'org.jetbrains:kotlin-react-redux:5.0.7-pre.72-kotlin-1.3.40'
    implementation 'org.jetbrains:kotlin-redux:4.0.0-pre.72-kotlin-1.3.40'
    implementation 'org.jetbrains:kotlin-styled:1.0.0-pre.72-kotlin-1.3.40'
    implementation 'org.jetbrains:kotlin-react-router-dom:4.3.1-pre.72-kotlin-1.3.40'
}

clean.doFirst() {
    delete("${web_dir}")
}


bundle.doLast() {
    copy {
        from "${buildDir}/resources/main/web"
        from "${buildDir}/bundle"
        into "${web_dir}"
    }
}

kotlinFrontend {
    npm {
        dependency "react"
        dependency "react-dom"
        dependency "react-router-dom"
        dependency "react-redux"
        dependency "redux"
        dependency "core-js"
        dependency "inline-style-prefixer"
        dependency "styled-components"
    }

    webpackBundle {
        bundleName = "this-will-be-overwritten" // NOTE: for example purposes this is overwritten in `webpack.config.d/filename.js`.
        contentPath = file('src/main/resources/web')
        if (project.hasProperty('prod')) {
            mode = "production"
        }
    }
}

compileKotlin2Js {
    kotlinOptions.moduleKind = 'commonjs'
}